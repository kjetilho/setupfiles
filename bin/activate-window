#! /usr/bin/perl
use strict;
use warnings;
use Getopt::Long;

sub usage() {
    print STDERR "Usage: $0 [-d] pattern\n";
    exit(64);
}
my $debug = 0;

GetOptions('debug|d+' => \$debug)
    or usage();
usage() unless @ARGV == 1;

my $pattern = shift;

open(my $wmctrl, '-|', 'wmctrl', '-l', '-G') or die "wmctrl -l: $!\n";

my @windows;
my @iwindows;

while (<$wmctrl>) {
    my ($id, $desktop, $x, $y, $w, $h, $host, $title) =
        (/^(\S+)\s+(-?\d+)\s+(-?\d+)\s+(-?\d+)\s+(\d+)\s+(\d+)\s+(\S+)\s+(.*)/);
    print "[${title}\@{$host}] $id on desktop $desktop at ${x}x${y}\n"
        if $debug > 1;
    if ($title =~ $pattern) {
        print "Found match, [${title}\@{$host}] $id on desktop $desktop at ${x}x${y}\n"
            if $debug;
        push(@windows, [ $id, $desktop, $x, $y, $w, $h ] );
    } elsif ($title =~ /$pattern/i) {
        print "Found MaTCh, [${title}\@{$host}] $id on desktop $desktop at ${x}x${y}\n"
            if $debug;
        push(@iwindows, [ $id, $desktop, $x, $y, $w, $h ] );
    }
}
close($wmctrl);

# Use case-insensitive matches if no case-sensitive match
@windows = @iwindows if @windows == 0;

if (@windows == 1 || @windows > 1) { # TODO: cycle through multiple matching windows
    my ($id, $desktop, $x, $y, $w, $h) = @{$windows[0]};
    system('wmctrl', '-i', '-a', $id);
    system('xdotool', 'mousemove', '--sync', int($x + $w/2), int($y + $h/2));
} else {
    print "No windows matched\n";
    exit(1);
}
