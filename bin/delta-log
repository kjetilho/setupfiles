#! /usr/bin/perl -w

use POSIX qw(strftime);
use Time::Local;
use Getopt::Long;

my $format = "%12.06f";
my $keep_timestamp;

sub usage {
    print STDERR "Usage: $0 [--keep-timestamp] [--format PRINTF-FORMAT]\n";
    print STDERR "Default PRINTF-FORMAT is '$format'\n";
    exit(64);
}

GetOptions('format=s' => \$format,
           'keep-timestamp!' => \$keep_timestamp,
    ) or usage();

my $previous_time;

while (<>) {
    if (/\b(\d{4})-(\d{2})-(\d{2}).(\d{2}):(\d{2}):(\d{2})\.(\d+)/) {
        my $subsec = $7 / 10**(length $7);
        my $t = timelocal($6, $5, $4, $3, $2-1, $1-1900) + $subsec;
        my $delta = defined $previous_time ? $t - $previous_time : 0.0;
        $previous_time = $t;
        my $replacement = sprintf($format, $delta);
        $replacement .= " [$&]" if $keep_timestamp;
        s/\b(\d{4})-(\d{2})-(\d{2}).(\d{2}):(\d{2}):(\d{2})\.(\d+)/$replacement/e;
    }
    print;
}
